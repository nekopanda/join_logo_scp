# join_logo_scp用の実行内容スクリプト
#
# 内容：ロゴを使わず無音シーンチェンジだけで前後をカット
#       ATXを想定した設定
#
#----------------------------------------------
# ロゴは無効化
#----------------------------------------------
LogoOff
#----------------------------------------------
# 開始マージンの設定
#----------------------------------------------
# 開始マージンのフレーム（-1の時は自動設定）
Default base -1

# 開始マージン付近に候補があれば優先選択
Select S 0 -1.3 1.3 -fromabs $base -logoext 0 $base+1.3

Default len_headmute 1.8         # 先頭認識するシーンチェンジからの無音期間

# 設定ない時の自動検索
Default autobase 00:00:30        # 自動検索する最大秒数
If $base == -1
  Select S 0 0 0 -N 1 -Shift 0 0 $autobase -logoext 0 $autobase+1.3 -SMA 0 $len_headmute
EndIf

#------------------------------------------------------------------
# 複数話構成の分割を検出(divoff=0の時)
#   構成が見つからなければ divuse=0になる
#   構成が見つかったらdivuse=1となり、１話期間は$len_unitに設定される
#------------------------------------------------------------------
# 分割しない時はここを1に設定するか、divoffを起動オプションのフラグに追加する
Default divoff   0               # 複数話構成の検出禁止（0:許可 1:禁止）
# 分割する時に最終話の時間による強制分割いらない時は1を設定する
Default divlastoff   0           # 複数話構成の最終話強制分割（0:許可 1:禁止）

# 分割を許可する時、等間隔に長時間無音期間（無音期間は下記から自動検出）があるかで複数話構成を検出
Default len_unit      0.0        # １話期間（後で設定される）
Default len_divdet1chk 4.7       # １話終了後の無音と認識する期間1（検出用）
Default len_divdet1aft 4.7       # １話終了後の無音と認識する期間1（後側）
Default len_divdet1bef -1.1      # １話終了後の無音と認識する期間1（前側）
Default len_divdet2chk 6.1       # １話終了後の無音と認識する期間2（検出用）
Default len_divdet2aft 6.0       # １話終了後の無音と認識する期間2（後側）
Default len_divdet2bef 0.0       # １話終了後の無音と認識する期間2（前側）
Default len_divdet3chk 11.0      # １話終了後の無音と認識する期間3（検出用）
Default len_divdet3aft 10.0      # １話終了後の無音と認識する期間3（後側）
Default len_divdet3bef 0.0       # １話終了後の無音と認識する期間3（前側）
Default len_divdet4chk 13.0      # １話終了後の無音と認識する期間4（検出用）
Default len_divdet4aft 12.0      # １話終了後の無音と認識する期間4（後側）
Default len_divdet4bef 0.0       # １話終了後の無音と認識する期間4（前側）

Set     divuse         0         # 複数話期間検出
# 複数話構成を長時間無音位置から自動検出する処理
If !$divoff
  # 長時間の長さを変えて等間隔の長時間無音を探す
  # 本編中に長時間無音がある場合の対策で5回目は30分番組終了付近から検索
  Set repcount  0
  Repeat 5
    EvalNum repcount $repcount+1
    If $repcount==1 || $repcount==5    # 1回目と5回目
      Set len_check  $len_divdet1chk
      Set len_after  $len_divdet1aft
      Set len_before $len_divdet1bef
    ElsIf $repcount==2                 # 2回目
      Set len_check  $len_divdet2chk
      Set len_after  $len_divdet2aft
      Set len_before $len_divdet2bef
    ElsIf $repcount==3                 # 3回目
      Set len_check  $len_divdet3chk
      Set len_after  $len_divdet3aft
      Set len_before $len_divdet3bef
    Else                               # 4回目
      Set len_check  $len_divdet4chk
      Set len_after  $len_divdet4aft
      Set len_before $len_divdet4bef
    EndIf
    # 最初の終了位置検索を開始する位置設定
    Set det_continue 1
    Set pos_tmp_end1st 00:03:00       # 最初の終了位置検索開始
    If $repcount>=5                   # 5回目検索
      If ($divuse!=0)                 # 既に分割発見している時は検索しない
        Set det_continue 0
      Else
        Set pos_tmp_end1st 00:22:00   # 5回目用の最初の終了位置検索開始
      EndIf
    EndIf
    # 最初の長時間無音位置を調査
    If $det_continue
      GetPos S ${pos_tmp_end1st} ${pos_tmp_end1st} 00:29:30 -N 1 -RSMA $len_before $len_check
      If $LASTEXE
        Set pos_tmp1 $POSHOLD                     # 最初の長時間無音位置
        Set pos_tmp2 $POSHOLD+$POSHOLD            # ２番目の長時間無音位置（予想）
      Else
        Set det_continue 0       # 無音位置なく複数話構成ではない
      EndIf
    EndIf
    # ２番目の長時間無音位置を調査
    If $det_continue
      GetPos S ${pos_tmp2}-00:00:30 ${pos_tmp2}-00:02:00 ${pos_tmp2}+00:10:00 -N 1 -RSMA $len_before $len_check
      If $LASTEXE
        Set      pos_tmp2 $POSHOLD                 # ２番目の長時間無音位置（確定）
        EvalTime len_tmpu ${pos_tmp2}-${pos_tmp1}  # １話分の期間（確定）
        Set      pos_tmp3 ${pos_tmp2}+${len_tmpu}  # ３番目の長時間無音位置（予想）
      Else
        Set det_continue 0       # 無音位置なく複数話構成ではない
      EndIf
    EndIf
    # ３番目の長時間無音位置を調査（予想位置にあれば分割を決定）
    If $det_continue
      GetPos S ${pos_tmp3} ${pos_tmp3}-00:00:30 ${pos_tmp3}+00:00:30 -N 1 -RSMA $len_before $len_check
      # ３番目が正確な位置になければ範囲を広げ４番目まで確認する
      If !$LASTEXE && $len_tmpu>00:20:00
        GetPos S ${pos_tmp3} ${pos_tmp3}-00:03:00 ${pos_tmp3}+00:03:00 -N 1 -RSMA $len_before $len_check
        If $LASTEXE                 # 範囲を広げて存在したら４番目も確認
          Set pos_tmp3  $POSHOLD
          Set pos_tmp4  ${pos_tmp3}+${len_tmpu}  # ４番目の長時間無音位置（予想）
          GetPos S ${pos_tmp4} ${pos_tmp4}-00:03:00 ${pos_tmp4}+00:03:00 -N 1 -RSMA $len_before $len_check
        EndIf
      EndIf
      If !$LASTEXE
        Set det_continue 0       # 無音位置なく複数話構成ではない
      EndIf
    EndIf
    # 予想位置にあれば分割決定
    If $det_continue
      Set divuse         1
      Set len_unit       $len_tmpu
    EndIf
  EndRepeat
EndIf

# 15分x2話のケース対応（誤検出可能性から通常は未使用。フラグ設定時のみ動作）
Default div15minute   0       # このフラグを事前に設定した時のみ動作
If !$divoff && !$divuse
  If $div15minute
    GetPos S 00:15:00 00:14:30 00:15:30 -N 1 -LenN 00:20:00 -1 -flat -RSMA 0 $len_headmute
    If $LASTEXE
      Set divuse 1            # 複数話構成を検出
      Set len_unit 00:15:00   # 15分固定
    EndIf
  EndIf
EndIf

# 自動検出できなかった場合は30分固定でもう一度検出
If !$divoff && !$divuse
  GetPos S 00:30:00 00:29:30 00:30:30 -N 1 -LenN 00:50:00 -1 -flat -RSMA 0 $len_headmute
  If $LASTEXE
    Set divuse 1            # 複数話構成を検出
    Set len_unit 00:30:00   # 30分固定
  EndIf
EndIf

#----------------------------------------------
# 時間による設定
#----------------------------------------------
Default eachframe $MAXTIME
If $divuse
  Set eachframe $len_unit
EndIf

If $eachframe >= 00:14:00
  Default sft_c   00:03:00       # 終了位置（優先位置）
  Default sft_l   00:08:00       # 終了位置（手前）
  Default sft_r   00:00:10       # 終了位置（最終）
ElsIf $eachframe >= 00:07:00
  Default sft_c   00:02:00       # 終了位置（優先位置）
  Default sft_l   00:05:00       # 終了位置（手前）
  Default sft_r   00:00:10       # 終了位置（最終）
ElsIf $eachframe >= 00:04:00
  Default sft_c   00:00:30       # 終了位置（優先位置）
  Default sft_l   00:03:30       # 終了位置（手前）
  Default sft_r   00:00:10       # 終了位置（最終）
Else
  Default sft_c   00:00:30       # 終了位置（優先位置）
  Default sft_l   00:02:30       # 終了位置（手前）
  Default sft_r   00:00:10       # 終了位置（最終）
EndIf

# 優先する無音位置検索用
Default sft_m $sft_l             # 終了位置（無音検索優先位置）
If ($eachframe>=00:22:30 && $eachframe<=00:30:30)  # 短い終了区間の補正
  EvalTime sft_m $eachframe-00:22:30
EndIf

# 終了位置検出用の無音期間
Default len_enddetect_aft 4.7    # 実際の終了付近と認識する無音期間（後側）
Default len_enddetect_bef -1.1   # 実際の終了付近と認識する無音期間（前側）

#------------------------------------
# 複数話構成の分割処理
#------------------------------------
# ・複数話構成で区切り地点に２秒($len_headmuteの値)以上の無音があればロゴを分割
Default divexe   0               # 分割実行確認フラグ
If $divuse
  Echo 複数話分割を実行（１話あたりの期間 = $len_unit）
  Set divexe   1                            # 分割実行確認フラグ
  Set len_uena  $len_unit                   # 分割に必要な最小期間
  EvalNum divmax ($MAXTIME/$len_unit)+2     # 話数最大検索（時間/時間は整数で実施）

  Set dualflag 0          # 長時間無音と次の開始を兼ねていたら1になる
  Set numdiv 1
  Repeat $divmax
    # 繰り返し分割実行（最初は長時間無音にかからないように）
    DivLogo S $len_unit $len_unit-00:00:15 $len_unit+00:00:15 -N $numdiv -LenN $len_uena -1 -flat -RSMA 0 $len_headmute -RNoSMA -2.0 0.0 -RNoSMA 2.0 4.7
    If !$LASTEXE          # なかったら長時間無音を気にせず検索
      DivLogo S $len_unit $len_unit-00:00:15 $len_unit+00:00:15 -N $numdiv -LenN $len_uena -1 -flat -RSMA 0 $len_headmute
      If $LASTEXE
        Set dualflag 1    # 長時間無音と次の開始の兼用あり
      EndIf
    EndIf
    If !$LASTEXE          # なかったら範囲を少し広げて検索
      If $dualflag        # 長時間無音兼用あり構成の検索
        DivLogo S $len_unit $len_unit-00:01:05 $len_unit+00:01:05 -N $numdiv -LenN $len_uena+$len_unit -1 -flat -RSMA -2.0 $len_headmute -RNoSMA 2.0 4.7
      EndIf
      If !$LASTEXE        # なかったら長時間無音兼用なしで検索
        DivLogo S $len_unit $len_unit-00:01:05 $len_unit+00:01:05 -N $numdiv -LenN $len_uena -1 -flat -RSMA 0 $len_headmute -RNoSMA -2.0 0.0 -RNoSMA 2.0 4.7
      EndIf
    EndIf
    If !$LASTEXE && $len_unit>00:20:00  # なかったら長時間無音にかからないように更に範囲を少し広げて検索
      If $dualflag        # 長時間無音兼用あり構成で残り期間長ければ長時間無音許可で検索
        DivLogo S $len_unit $len_unit-00:03:00 $len_unit+00:03:00 -N $numdiv -LenN $len_uena+$len_unit -1 -flat -RSMA -2.0 $len_headmute -RNoSMA 2.0 4.7
      EndIf
      If !$LASTEXE        # なかったら長時間無音兼用なしで検索
        DivLogo S $len_unit $len_unit-00:03:00 $len_unit+00:03:00 -N $numdiv -LenN $len_uena+$len_unit -1 -flat -RSMA 0 $len_headmute -RNoSMA -2.0 0.0 -RNoSMA 2.0 4.7
      EndIf
    EndIf
    EvalNum numdiv ${numdiv}+1
  EndRepeat
  # 一番最後だけは無音区切りがなくても時間で分割
  If !$divlastoff
    DivLogo S $len_unit $len_unit-00:01:05 $len_unit+00:01:05 -N -1 -LenN $len_unit -1 -flat -RNoSMA -2.0 0.0
  EndIf
  # 長時間無音と次の開始を兼ねていたら終了検索地点も兼用
  If $dualflag
    Set sft_r   00:00:00       # 終了位置（最終）
    Set sft_m   00:01:00       # 終了位置（無音検索優先位置）
  EndIf
EndIf

# ・45分以上で分割なければ終了位置を多めに検索
If $MAXTIME >= 00:45:00
  If !$divexe
    Set sft_c   00:03:00       # 終了位置（優先位置）
    Set sft_l   00:14:00       # 終了位置（手前）
    Set sft_r   00:00:10       # 終了位置（最終）
    Set sft_m   $sft_l         # 終了位置（無音検索優先位置）
  EndIf
EndIf

#------------------------------------
# 開始位置検出
#------------------------------------
# ・20秒無音が続き正確に20秒後無音シーンチェンジあれば開始位置として設定
Find S 20.0 19.8 20.2 -SMA 0.0 18.0+$len_headmute
# ・20秒以上無音が続けば開始位置として設定（放送休止のお知らせをカット）
Force S 20.0 -SMA 0 18.0+$len_headmute
# ・正確に２秒後無音シーンチェンジあれば開始位置として設定
Find S 2.0 1.8 2.2
# ・２秒以上無音が続けば開始位置として設定
Force S 2.0 -SMA 0 $len_headmute
# ・開始位置を設定
Find S 0 -1.3 1.3

#------------------------------------
# 終了位置検出
#------------------------------------
# 終了付近の位置認識
GetList E -$sft_m -$sft_l -$sft_r -RSMA $len_enddetect_bef $len_enddetect_aft
# Echo $LISTHOLD

# 念のため２回実行（１回目:実際の終了付近だけに限定、２回目：限定なし）
Set repcount 0
Repeat 2
EvalNum repcount $repcount+1

If $repcount==1
  Set endcommon "E -$sft_c -$sft_l -$sft_r -relative -TgtLimit -00:00:30 00:00:30"
Else
  Set endcommon "E -$sft_c -$sft_l -$sft_r -relative"
EndIf

# ・10秒前に無音シーンチェンジがあり、10秒間無音SCなく、-10〜5秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -10.2 -9.8 -NoSC -9.5 -0.5 -SMA -10.0 5.0

# ・10秒前に無音シーンチェンジがあり、10秒間無音SCなく、-10〜0,31〜35秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -10.2 -9.8 -NoSC -9.5 -0.5 -SMA -10.0 0.0 -SMA 31.0 35.0

# ・４秒前、１秒後に無音シーンチェンジがあり、-4〜10秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -4.2 -3.8 -SC 0.8 1.2 -SMA -5.0 10.0

# ・５秒前に無音シーンチェンジがあり、-5〜9秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -5.2 -4.8 -SMA -5.0 9.0

# ・６秒前に無音シーンチェンジがあり、６秒間無音SCなく、-6〜10秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -6.2 -5.8 -NoSC -5.5 -0.5 -SMA -5.9 10.0

# ・無音開始から１秒以内で、4,5,6秒後に無音SCなく、10秒以上無音が続くシーンチェンジ位置
Find $endcommon -SMA 0.0 10.0 -NoSM -1.0 -1.0 -NoSC 3.8 4.2 -NoSC 4.8 5.2 -NoSC 5.8 6.2

# ・５秒前に無音シーンチェンジがあり、１秒前になく、-5〜8秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -5.2 -4.8 -NoSC -1.2 -0.8 -SMA -5.0 8.0

# ・無音開始から２秒以内で、4,5,6秒後に無音SCなく、10秒以上無音が続くシーンチェンジ位置
Find $endcommon -SMA 0.0 10.0 -NoSM -2.0 -2.0 -NoSC 3.8 4.2 -NoSC 4.8 5.2 -NoSC 5.8 6.2

# ・５秒前に無音シーンチェンジがあり、１秒前になく、-5〜7秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -5.2 -4.8 -NoSC -1.2 -0.8 -SMA -5.0 7.0

# ・無音開始から３秒以内で、4,5,6秒後に無音SCなく、10秒以上無音が続くシーンチェンジ位置
Find $endcommon -SMA 0.0 10.0 -NoSM -3.0 -3.0 -NoSC 3.8 4.2 -NoSC 4.8 5.2 -NoSC 5.8 6.2

# ・３秒前、１秒後に無音シーンチェンジがあり、-3〜7秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -3.2 -2.8 -SC 0.8 1.2 -SMA -3.0 7.0

# ・４秒前、１秒後に無音シーンチェンジがあり、-4〜7秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -4.2 -3.8 -SC 0.8 1.2 -SMA -4.0 7.0

# ・５秒前に無音シーンチェンジがあり、-5〜6秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -5.2 -4.8 -SMA -5.0 6.0

# ・６秒前に無音シーンチェンジがあり、-6〜6秒まで無音が続くシーンチェンジ位置
Find $endcommon -SC -6.2 -5.8 -SMA -5.9 6.0

# ・無音開始から１秒以内で、5秒以上無音が続くシーンチェンジ位置
Find $endcommon -SMA 0.0 5.0 -NoSMA -1.0 0.0

# ・無音開始から２秒以内で、5秒以上無音が続くシーンチェンジ位置
Find $endcommon -SMA 0.0 5.0 -NoSMA -2.0 0.0

# ・無音開始から３秒以内で、5秒以上無音が続くシーンチェンジ位置
Find $endcommon -SMA 0.0 5.0 -NoSMA -3.0 0.0

# ・無音開始から５秒以内で、5秒以上無音が続くシーンチェンジ位置
Find $endcommon -SMA 0.0 5.0 -NoSMA -5.0 0.0

# ・無音開始から５秒以内で、検出設定秒以上無音が続くシーンチェンジ位置
Find $endcommon -SMA $len_enddetect_bef $len_enddetect_aft -NoSMA -5.0 0.0

EndRepeat

# ・複数話構成で一番最後の位置検出なければ最後の構成は無効化
If $divexe
  Abort E -N -1 -WithP
EndIf

# ・末尾に一番近い無音シーンチェンジで終了
Find E 0 -1.2 0 -noedge
Find E 0 -120.0 0

#----------------------------------------------
# 予告構成の推測
#----------------------------------------------
#--- ファイル最後部分の予告検出用 ---
Default cuttr_code     3    # 動作設定（0:検出なし 1:CM明けから数える 2:CM明け／CM直前を自動 3:CM直前から数える）
Default cuttr_limit    5    # 予告と認識させる構成数（0:予告なし 1〜:構成数）
Default cuttr_trscope  42   # 予告をCM直前から検索する場合のCM前最大検索秒数

AutoUp B -code 2
AutoCut TR -autoeach -code $cuttr_code -limit $cuttr_limit -trscope $cuttr_trscope
