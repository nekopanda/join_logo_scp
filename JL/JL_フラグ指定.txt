# join_logo_scp用の実行内容スクリプト
#
# 内容：フラグ設定情報を使って不要部分のカットを行う
#       標準と基本構成は同じ、動作記述内でフラグ指定動作を追加で記載
#
#
#----------------------------------------------
# 開始・終了マージンの設定
#----------------------------------------------
#--- 開始マージンの設定 ---
Default base               -1     # 開始マージンのフレーム（-1の時は自動検出）
Default param_zone_first   0.0    # 最初から指定期間内に全体が収まる構成はロゴなし扱いとする
Default param_lgcut_first  12.0   # 最初からロゴありの時開始マージン最大期間（-1の時はロゴなしと同じ）

#--- 終了マージンの設定 ---
Default param_wcomp_last   30.5   # 最後構成ロゴなし扱いとする最大期間
Default param_zone_last    0.0    # 最後から指定期間内に全体が収まる構成はロゴなし扱いとする（-1の時は最後構成の分解を抑制）

#----------------------------------------------
# 番組構成から判断するカット位置設定
#----------------------------------------------
#----- ロゴ情報の優先度 ---
Default logo_level    6     # 推測時のロゴ使用（1:なし 2-8:ロゴ情報使用-大きい程ロゴ情報優先）
Default logo_revise   23    # ロゴ使用関連の構成変更（0-4:ロゴ端の削除-大きい程削除 +10:ロゴ内削除あり +20:ロゴ外広域削除あり）

#----- ロゴなし時のカット -----
Default autocm_code   6     # ロゴなし時の15秒単位CM検出（0:なし 1-10:CM検出-大きい程強く検出）
Default autocm_sub    10    # ロゴなし時の補助設定（0:なし 1:先頭15秒単位構成は少数でもCM化 +10:45秒は含めない）
Default autocm_scope  40    # 最後から指定秒以降の位置を含む短時間構成は手前がCM扱いならCM継続
Default autocm_scopex 0     # 最後から指定秒以降の位置に含まれる60/90/120秒構成はCM扱い
Default use_autocmtr  1     # ロゴなし時の予告検出拡張（0:なし 1:あり）

#----- カット有無選択 -----
Default param_adduc   0     # ロゴなし不明構成（0:制御なし 1:残す）
Default param_cuttr   0     # 予告等終了後のロゴ付き15秒単位構成（0:残す 1:カット）
Default param_cutsp   0     # 番組提供カット（0:残す 1:カット）※誤検出の可能性から残すを推奨
Default param_nosc    0     # シーンチェンジなし無音位置のCM判断（0:自動 1:使用しない 2:使用する）
Default param_cntsc   0     # 無音シーンチェンジ数が多い構成内の分離（0:なし 1:あり）

#----- ファイル最後の予告位置認識 -----
Default cuttr_code    2    # 動作設定（0:検出なし 1:CM明けから数える 2:CM明け／CM直前を自動 3:CM直前から数える）
Default cuttr_limit   1    # 予告と認識させる構成数（0:予告なし 1〜:構成数）
Default use_addtr     1    # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）

#--- エンドカードの認識（15秒未満構成のカット位置検出） ---
If $NOLOGO
  Default addec_code1   11003 # エンドカード検出1-優先度1（ロゴなし時の制約緩める）
EndIf
Default cutec_code    1     # カット動作設定（0:カットなし 1:カットあり）
Default addec_code1   31303 # エンドカード検出1-優先度1
Default addec_code2   11223 # エンドカード検出2-優先度2
Default addec_code3   11023 # エンドカード検出3-優先度3
Default addec_code4   11203 # エンドカード検出4-優先度4
Default addec_code5   0     # エンドカード検出5-優先度5
Default addec_period1 5     # エンドカード検出1の構成指定秒数
Default addec_period2 5     # エンドカード検出2の構成指定秒数
Default addec_period3 3     # エンドカード検出3の構成指定秒数
Default addec_period4 10    # エンドカード検出4の構成指定秒数
Default addec_period5 5     # エンドカード検出5の構成指定秒数
#
#（addec_code*の設定）
#   0     : 検出しない
#   3     : 構成period秒以下（下2桁目＝0の時、予告・番組提供に隣接）
#  +20    : 予告・番組提供から検索90秒以内に拡張
#  +200   : ロゴ付きのみ検索
#  +300   : 隣接まで含めロゴなしのみ検索
#  +1000  : 予告・番組提供の後側に限定
#  +10000 : 構成期間13秒以下で最初に候補となる位置限定
#  +20000 : 既存予告が30秒以上ある時は無効とする
#  +30000 : +10000と+20000の両方
# 具体的な想定
#   31303 : 番組提供直後の隣接で番組提供・エンドカード共にロゴなし、予告は30秒未満
#   11303 : 番組提供直後の隣接で番組提供・エンドカード共にロゴなし
#   11223 : 予告・番組提供から90秒以内のロゴあり最初の短期間構成
#   11023 : 予告・番組提供から90秒以内の最初の短期間構成
#   11203 : 予告・番組提供直後の隣接でロゴあり
#

#----- 番組提供認識設定 -----
Default addsph_code1  1001  # 先頭番組提供-優先度1
Default addsph_code2  21    # 先頭番組提供-優先度2
Default addsph_code3  1004  # 先頭番組提供-優先度3
Default addsph_code4  206   # 先頭番組提供-優先度4
Default addsph_code5  0     # 先頭番組提供-優先度5
Default addspt_code1  421   # 最後番組提供-優先度1
Default addspt_code2  1004  # 最後番組提供-優先度2
Default addspt_code3  206   # 最後番組提供-優先度3
Default addspt_code4  4001  # 最後番組提供-優先度4
Default addspt_code5  0     # 最後番組提供-優先度5
Default addsph_scope  60    # 先頭番組提供の期間指定時検索秒数
Default addspt_scope  120   # 最後番組提供の期間指定時検索秒数

#（番組提供認識の主な設定値）
# [code]
#    0 : 検出しない
#    1 : 期間6-13秒（隣接）を検索（下2桁目＝0の時、予告（なければロゴ）に隣接部分）
#   21 : 期間6-13秒（範囲90秒）（下2桁目＝2の時、検索範囲を予告（なければロゴ）から90秒以内に広げる）
#  206 : 期間15秒（隣接・ロゴ付き）を検索（下3桁目＝2の時、ロゴ付きに限定。下1桁＝6は期間15秒限定）
#  421 : 期間6-13秒（範囲90秒・カット位置後含む）（下3桁目＝4の時、AutoCutによるカット位置後も検索）
# 1001 : 期間6-13秒（ロゴ終了側隣接）を検索（下4桁目＝1の時、前側（ロゴ開始側）は候補から外す）
# 1004 : 期間5-13秒（隣接）を検索（下1桁＝4の時、期間5秒を追加）
# 4001 : 期間6-13秒（予告と認識した最初の内容を番組提供化＋予告を後ろにずらす）
# [scope]
#   下2桁目で検索範囲指定時に上記説明90秒の所を秒数変更
#

#----------------------------------------------
# 各ロゴ期間共通の開始・終了部分カット
#----------------------------------------------
Default edgest_code   0         # 各ロゴ開始部分のカット動作（0:カットなし 2:指定秒構成時カット）
Default edgest_period 15        # 各ロゴ開始部分のカットする秒数
Default edgeed_code   0         # 各ロゴ終了部分のカット動作（0:カットなし 2:指定秒構成時カット）
Default edgeed_period 15        # 各ロゴ終了部分のカットする秒数

#----- 最初のロゴ先頭部分カット -----
Default cut1st_code   0   # 動作設定（0:カットなし 2:指定秒数の構成時カット）
Default cut1st_period 3   # カット秒数指定

#----------------------------------------------
# ロゴ位置情報
#----------------------------------------------
OptDefault -CutMrgWI 2          # CutMrgIn値の使用判断（0:指定値 1:指定値と0の２点から判断 2:自動判断）
OptDefault -CutMrgWO 2          # CutMrgOut値の使用判断（0:指定値 1:指定値と0の２点から判断 2:自動判断）

#----------------------------------------------
# 内部パラメータ情報
#----------------------------------------------
Default param_wlogo_trmax  00:02:10   # 予告番宣部分と認識する最大ロゴ期間
Default param_wcomp_trmax  00:01:10   # 予告番宣部分と認識する１構成あたり最大期間
Default param_wlogo_sftmrg 3.5        # ロゴ期間が通常より少し短い場合に延長補正する最大期間
Default param_wlogo_summin 00:00:20   # ロゴ期間合計が指定期間より短い場合はロゴなしとする
Default param_wlogo_lgmin  20.0       # 推測時のロゴ有情報確定と認識する期間
Default param_wlogo_cmmin  40.0       # 推測時のロゴ無情報確定と認識する期間
Default param_wlogo_revmin 125.0      # CM構成ではないロゴなし期間が不明構成と認識する構成最小期間
Default param_mgn_cmdetect 0.25       # CM構成で15秒単位ではない可能性と認識する誤差フレーム期間
Default param_mgn_cmdivide 0.1        # CM構成内部の分割を許可する誤差フレーム期間
#--- 開始マージン詳細 ---
Default cutbaselogo        1          # 前番組から続くロゴ（0:制御なし 1:カット許可）
Default param_pos_first    $base      # 開始マージン位置
Default param_lvpos_first  $cutbaselogo # 開始マージン設定（0:制御なし 1:先頭からのロゴ無視 2:指定位置最優先 3:Select位置優先）
Default param_wcomp_first  30.0       # 先頭構成ロゴなしとする最大期間


############# 以降は詳細設定と実際の動作記述 ############

#----------------------------------------------
# フラグまとめ設定
#----------------------------------------------
#***************** フラグ内容 ********************************************
#   ANIMAX   : ANIMAX用設定
#*************************************************************************
# ・ANIMAX用の設定
If ANIMAX
  Set ASPN     1               # 番組提供なし
#  Set TWTR15A  1              # 予告が見つからない時、15秒以下構成で次構成が10秒なら予告にする
  OptSet -CutMrgIn   150       # 切り替わりからロゴ表示開始までの期間
  OptSet -CutMrgOut  150       # ロゴ表示終了から切り替わりまでの期間
  If $NOLOGO
    Set NLANIMAX      1        # ロゴなしANIMAX設定
    Set autocm_scopex 120      # 最後から指定秒以降の位置に含まれる60/90/120秒構成はCM扱い
  EndIf
EndIf

#----------------------------------------------
# 内部パラメータ設定
#----------------------------------------------
SetParam CutTR       $param_cuttr
SetParam CutSP       $param_cutsp
SetParam AddUC       $param_adduc
SetParam TypeNoSc    $param_nosc
SetParam CancelCntSc $param_cntsc
SetParam WLogoTRMax  $param_wlogo_trmax
SetParam WCompTRMax  $param_wcomp_trmax
SetParam WCompLast   $param_wcomp_last
SetParam WLogoSftMrg $param_wlogo_sftmrg
SetParam WLogoSumMin $param_wlogo_summin
SetParam WLogoLgMin  $param_wlogo_lgmin
SetParam WLogoCmMin  $param_wlogo_cmmin
SetParam WLogoRevMin $param_wlogo_revmin
SetParam MgnCmDetect $param_mgn_cmdetect
SetParam MgnCmDivide $param_mgn_cmdivide
SetParam WCompFirst  $param_wcomp_first
SetParam LgCutFirst  $param_lgcut_first
SetParam ZoneFirst   $param_zone_first
SetParam ZoneLast    $param_zone_last
SetParam LvPosFirst  $param_lvpos_first
SetParam PosFirst    $param_pos_first
SetParam LogoLevel   $logo_level
SetParam LogoRevise  $logo_revise
SetParam AutoCmSub   $autocm_sub

#----------------------------------------------
# 開始マージン選択動作
#----------------------------------------------
# 過去互換用(SetParam LvPosFirst 3)
# 開始マージン付近に候補があれば開始位置として優先させる
#If $cutbaselogo
#  Select S 0 -2.2 2.2 -fromabs $base -logoext 0 $base+2.2
#Else
#  Select S 0 -2.2 2.2 -fromabs $base
#EndIf

#----------------------------------------------
# 最後のロゴなし部分の予告を追加認識
#----------------------------------------------
Default ti_scope      60.0  # 検索するロゴからの位置（秒数指定の場合は小数付きで記載）

#***************** フラグ内容 ********************************************
#   TI25    : 最終ロゴ終了から60秒離れた構成25秒をロゴありとして追加
#   TI15    : 最終ロゴ終了から60秒離れた構成15秒をロゴありとして追加
#   TIB     : 最終ロゴ終了から60秒離れた所にある3,5,10秒をロゴありとして追加
#   HI210   : 先頭ロゴから210秒はロゴありとする
#   HI240   : 先頭ロゴから240秒はロゴありとする
#*************************************************************************
If TI25
  MkLogo E $ti_scope $ti_scope-0.2 $ti_scope+0.2 -N -1 -EndLen 25.0 24.8 25.2 -Ftail 00:00:00 00:07:00 -noedge
EndIf

If TI15
  MkLogo E $ti_scope $ti_scope-0.2 $ti_scope+0.2 -N -1 -EndLen 15.0 14.8 15.2 -Ftail 00:00:00 00:07:00 -noedge
EndIf

If TIB
  MkLogo E $ti_scope $ti_scope-0.2 $ti_scope+0.2 -N -1 -EndLen 5.0 4.8 5.2   -Ftail 00:00:00 00:07:00 -noedge
  MkLogo E $ti_scope $ti_scope-0.2 $ti_scope+0.2 -N -1 -EndLen 3.0 2.8 3.2   -Ftail 00:00:00 00:07:00 -noedge -else
  MkLogo E $ti_scope $ti_scope-0.2 $ti_scope+0.2 -N -1 -EndLen 10.0 9.8 10.2 -Ftail 00:00:00 00:07:00 -noedge -else
EndIf

If HI210
  MkLogo S 0 -30.0 30.0 -N 1 -EndLen 00:03:30 00:03:26 00:03:34 -overlap -confirm -noedge
EndIf

If HI240
  MkLogo S 0 -30.0 30.0 -N 1 -EndLen 00:04:00 00:03:56 00:04:04 -overlap -confirm -noedge
EndIf

#----------------------------------------------
# ロゴなし時の15秒単位CM検出
#----------------------------------------------
Default autocm_maxprd 60    # 指定秒を超える構成は15秒単位でもCM扱いにしない

#--- ロゴなし時のみ15秒単位構成をCM化 ---
AutoCM B -code $autocm_code -scope $autocm_scope -scopex $autocm_scopex -maxprd $autocm_maxprd

#----------------------------------------------
# 最初のロゴ部分カット・延長
#----------------------------------------------
Default addsph_period1 5       # 先頭番組提供の構成期間限定時用（優先順位1のみ）
Default addsph_maxprd1 13      # 先頭番組提供の構成期間限定時用（優先順位1のみ）
Default fhead_st     $base     # 最初の番組提供と認識する先頭フレームからの位置（起点）
Default fhead_ed     00:13:30  # 最初の番組提供と認識する先頭フレームからの位置（終点）
Default fhead_name   -Fhead    # 位置を前半に絞る
Default cut1st_direct  0.0     # 推測構成で見つからない時の最初のロゴ先頭部分カット期間

#***************** フラグ内容 ********************************************
#   ASPL   : 番組提供は16〜25秒を検索
#   ASPA   : 番組提供は6〜13秒のみ認識
#   ASPN   : 番組提供なし
#   HC10   : 最初の10秒は不要
#   HC5    : 最初の5秒は不要
#   HC4    : 最初の4秒は不要
#   HC3    : 最初の3秒は不要
#   HCA    : 最初の3〜10秒で無音シーンチェンジがあれば不要
#   HCCM   : 最初に不要カットするロゴ付番宣（15秒か30秒）から始まる
#   HCSP   : 最初の番組提供認識をしない
#   HRSP15 : 最初の番組提供を15秒最優先で検索
#   HFSPL  : 最初の番組提供認識位置を20分近くまで拡張
#*************************************************************************

# ・番組提供を16〜25秒で検索
If ASPL
  Set addsph_code1    5
  Set addsph_period1  16
  Set addsph_maxprd1  25
EndIf

# ・番組提供は6〜13秒のみ認識
If ASPA
  Set addsph_code1  1001
  Set addsph_code2  21
  Set addsph_code3  0
  Set addsph_code4  0
  Set addsph_code5  0
EndIf

# ・番組提供を15秒で検索
If HRSP15
  Set addsph_code1    6
EndIf

# ・最初にロゴ付番宣が入っている場合のカット（最大30秒で30秒か15秒構成をカット）
If HCCM
  AutoEdge S -N 1 -code 1002 -scope 30 -period 30
  AutoEdge S -N 1 -code 1002 -scope 30 -period 15 -else
  Set cut1st_code 0     # 動作設定（0:カットなし）
EndIf

# ・最初のロゴ表示開始直後の10秒無音シーンチェンジはカット（局タイトルを想定）
If HC10
  Set cut1st_code   2   # 動作設定（2:指定秒数の構成時カット）
  Set cut1st_period 10  # カット秒数指定
EndIf

# ・最初のロゴ表示開始直後の5秒無音シーンチェンジはカット（局タイトルを想定）
If HC5
  Set cut1st_code   2   # 動作設定（2:指定秒数の構成時カット）
  Set cut1st_period 5   # カット秒数指定
  Set cut1st_direct 5.0 # 推測構成で見つからない時のカット期間指定
EndIf

# ・最初のロゴ表示開始直後の3秒無音シーンチェンジはカット（局タイトルを想定）
If HC4
  Set cut1st_code   2   # 動作設定（2:指定秒数の構成時カット）
  Set cut1st_period 4   # カット秒数指定
  Set cut1st_direct 4.0 # 推測構成で見つからない時のカット期間指定
EndIf

# ・最初のロゴ表示開始直後の3秒無音シーンチェンジはカット（局タイトルを想定）
If HC3
  Set cut1st_code   2   # 動作設定（2:指定秒数の構成時カット）
  Set cut1st_period 3   # カット秒数指定
  Set cut1st_direct 3.0 # 推測構成で見つからない時のカット期間指定
EndIf

# ・最初のロゴ表示開始直後の3-10秒無音シーンチェンジはカット
If HCA
  Set cut1st_code   1   # 動作設定（1:3-10秒の構成時カット）
EndIf

# ・最初の番組提供認識をしない
If HCSP ASPN
  Set addsph_code1  0
  Set addsph_code2  0
  Set addsph_code3  0
  Set addsph_code4  0
  Set addsph_code5  0
EndIf

# ・最初の番組提供認識位置を20分近くまで拡張
If HFSPL
  Set fhead_ed     00:20:00  # 最初の予告と認識する先頭フレームからの位置（終点）
  Set fhead_name   -F        # 前半縛りなし
EndIf

#***************** ここまでフラグ内容 *****************

#--- 先頭ロゴのカット ---
AutoEdge S -N 1 -code $cut1st_code -period $cut1st_period
If !$LASTEXE                 # カットしなかった場合
  If $cut1st_direct > 0.0    # 推測構成を使わずカットし推測構成に反映する処理
    Find S $cut1st_direct -0.5+$cut1st_direct 0.5+$cut1st_direct -N 1 -autochg
  EndIf
EndIf

#--- ファイル先頭部分の番組提供認識 ---
AutoAdd SP $fhead_name $fhead_st $fhead_ed -code $addsph_code1 -limit 1 -scope $addsph_scope -period $addsph_period1 -maxprd $addsph_maxprd1
AutoAdd SP $fhead_name $fhead_st $fhead_ed -code $addsph_code2 -limit 1 -scope $addsph_scope
AutoAdd SP $fhead_name $fhead_st $fhead_ed -code $addsph_code3 -limit 1 -scope $addsph_scope
AutoAdd SP $fhead_name $fhead_st $fhead_ed -code $addsph_code4 -limit 1 -scope $addsph_scope
AutoAdd SP $fhead_name $fhead_st $fhead_ed -code $addsph_code5 -limit 1 -scope $addsph_scope

#----------------------------------------------
# 最後のロゴ位置カット
#----------------------------------------------
Default cuttr_trscope  27   # 予告をCM直前から検索する場合のCM前最大検索秒数
Default cuttr_tr1stprd 45   # 予告開始と認識する構成の最大秒数（指定秒数以下の構成が現れるまで予告にしない）
Default cutec_limit    0    # エンドカードカット位置指定（0:追加なし 1:1構成残す -1:最後の１つ強制的にカット）
Default cutec_period   5    # エンドカードカット時に期間指定する場合の秒数
Default addec_scope1   90   # エンドカード検出1の期間指定時検索終了秒数(code下2桁目=2以上の時使用)
Default addec_scope2   90   # エンドカード検出2の期間指定時検索終了秒数(code下2桁目=2以上の時使用)
Default addec_scope3   90   # エンドカード検出3の期間指定時検索終了秒数(code下2桁目=2以上の時使用)
Default addec_scope4   90   # エンドカード検出4の期間指定時検索終了秒数(code下2桁目=2以上の時使用)
Default addec_scope5   90   # エンドカード検出5の期間指定時検索終了秒数(code下2桁目=2以上の時使用)
Default addec_scopen1  0    # エンドカード検出1の期間指定時検索開始秒数(code下2桁目=2以上の時使用)
Default addec_scopen2  0    # エンドカード検出2の期間指定時検索開始秒数(code下2桁目=2以上の時使用)
Default addec_scopen3  0    # エンドカード検出3の期間指定時検索開始秒数(code下2桁目=2以上の時使用)
Default addec_scopen4  0    # エンドカード検出4の期間指定時検索開始秒数(code下2桁目=2以上の時使用)
Default addec_scopen5  0    # エンドカード検出5の期間指定時検索開始秒数(code下2桁目=2以上の時使用)
Default addec_trsumprd 30   # エンドカード検出で既存予告期間により中止する場合の指定秒数
Default addtr_code1    21   # 予告が見つからない時のロゴなし予告検出1（0:検出しない 1:隣接15秒未満 21:検索60秒地点構成15秒未満）
Default addtr_code2    25   # 予告が見つからない時のロゴなし予告検出2（0:検出しない 25:検索60秒地点構成16〜29秒）
Default addtr_limit    $cuttr_limit    # 予告が見つからないと判断する必要予告構成数（0:制限なし 1〜:構成数）
Default addtr_scope    65   # 予告が見つからない時のロゴなし予告検出検索秒数（最大）
Default addtr_scopen   58   # 予告が見つからない時のロゴなし予告検出検索秒数（開始隣接側）
Default addtr_secnext  0    # ロゴなし予告の次の構成を秒数指定で限定する（0の時指定しない）
Default addtr_period1  1    # ロゴなし予告検出1の期間指定時構成最小秒数(code下1桁=5の時)
Default addtr_maxprd1  14   # ロゴなし予告検出1の期間指定時構成最大秒数(code下1桁=5の時)
Default addtr_period2  16   # ロゴなし予告検出2の期間指定時構成最小秒数(code下1桁=5の時)
Default addtr_maxprd2  29   # ロゴなし予告検出2の期間指定時構成最大秒数(code下1桁=5の時)
Default addspt_period1 5    # 最後番組提供の構成期間限定時用（優先順位1のみ）
Default addspt_maxprd1 13   # 最後番組提供の構成期間限定時用（優先順位1のみ）
Default addspt_scopen  0    # 最後番組提供の期間指定時検索開始秒数
Default use_add0tr     0    # 追加の予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
Default add0tr_code1   0    # 追加の予告が見つからない時のロゴなし予告検出1（0:検出しない 1:隣接15秒未満 21:検索60秒地点構成15秒未満）
Default add0tr_code2   0    # 追加の予告が見つからない時のロゴなし予告検出2（0:検出しない 25:検索60秒地点構成16〜29秒）
Default add0tr_limit   $cuttr_limit    # 追加の予告が見つからないと判断する必要予告構成数（0:制限なし 1〜:構成数）
Default add0tr_scope   65   # 追加の予告が見つからない時のロゴなし予告検出検索秒数（最大）
Default add0tr_scopen  58   # 追加の予告が見つからない時のロゴなし予告検出検索秒数（開始隣接側）
Default add0tr_secnext 0    # 追加のロゴなし予告の次の構成を秒数指定で限定する（0の時指定しない）
Default add0tr_period1 1    # 追加のロゴなし予告検出1の期間指定時構成最小秒数(code下1桁=5の時)
Default add0tr_maxprd1 14   # 追加のロゴなし予告検出1の期間指定時構成最大秒数(code下1桁=5の時)
Default add0tr_period2 16   # 追加のロゴなし予告検出2の期間指定時構成最小秒数(code下1桁=5の時)
Default add0tr_maxprd2 29   # 追加のロゴなし予告検出2の期間指定時構成最大秒数(code下1桁=5の時)
Default acmtr_code1    4063 # ロゴなし時拡張予告検出
Default acmtr_period   45   # ロゴなし時拡張予告検出時の構成最大秒数(code下1桁=3の時)
Default acmtr_scope1   60   # ロゴなし時拡張予告検出時の検索位置1
Default acmtr_scope2   90   # ロゴなし時拡張予告検出時の検索位置2
Default acmtr_code2    4043 # ロゴなし時拡張予告検出2
Default ftail_st      00:00:00  # 最後の予告以降と認識する最終フレームからの位置（起点）
Default ftail_ed      00:07:00  # 最後の予告以降と認識する最終フレームからの位置（終点）
If use_addtr
  Default cuttr_addcode  100  # CM明け直前のロゴなし部分（0:検索なし 100:予告候補に追加許可）
Else
  Default cuttr_addcode  0    # CM明け直前のロゴなし部分（0:検索なし 100:予告候補に追加許可）
EndIf

#***************** フラグ内容 ********************************************
#   ASPL   : 番組提供は16〜25秒を検索
#   ASPA   : 番組提供は6〜13秒のみ認識
#   ASPN   : 番組提供なし
#   TCFULL : 最後のロゴ部分はすべてカットする（予告認識部分以降をすべてカット）
#   TCLAST : 最後のロゴ部分はすべてカットする（予告検出前に実行）
#   TCLF30 : 最後のロゴ部分が最後から30秒以内に始まる場合カットする（予告検出前に実行）
#   TCL60  : 最後のロゴ区間が60秒だった時カット（予告検出前に実行）
#   TC5    : 最後の5秒は不要
#   TCB    : 最後は自動であるが、終了直前の15秒未満１構成をカット
#   TCSP   : 最後の番組提供を認識をしない
#   TCCM   : 最後は不要カットするロゴ付番宣（15秒か30秒）で終わる
#   TCE    : エンドカードなしとする
#   TREB   : ロゴ有無に関係なく15秒未満１構成をエンドカードとして残す
#   TRE    : ロゴ有無に関係なく5秒以下１構成をエンドカードとして残す
#   TLE    : エンドカードはロゴありのみ検出
#   TN     : ロゴ通りに出力
#   TWTRLOW   : 予告検出推測は弱くする
#   TRSP15    : 最後の番組提供を15秒最優先で検索
#   TWSP15    : 最後の番組提供が見つからない時15秒で検索
#   TRSP15EN  : 最後の番組提供を予告後かつロゴ終了直後の15秒で検索
#   TSC15     : 予告部分のロゴ開始直後15秒はカット（予告検出前に実行）
#   TEC15     : 予告部分のロゴ終了直前15秒はカット（予告検出前に実行）
#   TPTRLIM0  : 予告構成数=0
#   TPTRLIM2  : 予告構成数=2
#   TPCUTTR0  : 予告等終了後のロゴ付き15秒単位構成を残す
#   TPCUTTR1  : 予告等終了後のロゴ付き15秒単位構成をカット
#   TWSP15R60 : 予告から60秒離れた所にある15秒構成を番組提供が見つからない時残す
#   TWTR15R60 : 予告が見つからない時のロゴなし予告検出に15秒構成を含める
#   TWTR15A   : TWTR15R60＋条件追加（次の構成が10秒、検索範囲は広げる）
#   TRE5R180  : ロゴ有無に関係なく5秒１構成をエンドカードとして残す（ロゴから３分以内）
#   TRTRNL15R0 : （ロゴなし時のみ）最終ロゴ扱い終了直後の構成15秒を予告（２回検索）
#   TRTRNLR0   : （ロゴなし時のみ）最終ロゴ扱い終了直後の構成15/30秒を予告
#   TRTRNLR30  : （ロゴなし時のみ）最終ロゴ扱い終了から30秒離れた構成15/30秒を予告
#   TRTRNLR60  : （ロゴなし時のみ）最終ロゴ扱い終了から60秒離れた構成15/30秒を予告
#   TRTRNLR90  : （ロゴなし時のみ）最終ロゴ扱い終了から90秒離れた構成15/30秒を予告
#   TRTRNLR120 : （ロゴなし時のみ）最終ロゴ扱い終了から120秒離れた構成15/30秒を予告
#   TRTRNLR150 : （ロゴなし時のみ）最終ロゴ扱い終了から150秒離れた構成15/30秒を予告
#*************************************************************************
#
# ・最後にロゴ付番宣が入っている場合のカット（最大30秒で30秒か15秒構成をカット）
If TCCM
  AutoEdge E -NR -1 -code 1002 -scope 30 -period 30
  AutoEdge E -NR -1 -code 1002 -scope 30 -period 15 -else
EndIf

# ・番組提供を16〜25秒で検索
If ASPL
  Set addspt_code1    5
  Set addspt_period1  16
  Set addspt_maxprd1  25
EndIf

# ・番組提供は6〜13秒のみ認識
If ASPA
  Set addspt_code1  21
  Set addspt_code2  0
  Set addspt_code3  0
  Set addspt_code4  0
  Set addspt_code5  0
EndIf

# ・番組提供を15秒で検索
If TRSP15
  Set addspt_code1  6
  Set ftail_ed      00:10:00  # 最後の予告以降と認識する最終フレームからの位置（終点）
EndIf

# ・番組提供が見つからない時15秒で検索
If TWSP15
  Set addspt_code5  6
  Set ftail_ed      00:10:00  # 最後の予告以降と認識する最終フレームからの位置（終点）
EndIf

# ・番組提供を予告後かつロゴ終了直後の15秒で検索
If TRSP15EN
  Set addspt_code1  1146      # 予告後(1000)ロゴなし(100)ロゴ隣接(40)15秒(6)
  Set addspt_code2  1106      # 予告後(1000)ロゴなし(100)予告隣接(00)15秒(6)
  Set ftail_ed      00:10:00  # 最後の予告以降と認識する最終フレームからの位置（終点）
EndIf

# ・CM明け以降最後のロゴ部分はすべてカットする
If TCFULL
  Set cuttr_code    1    # 動作設定（1:CM明けから数える）
  Set cuttr_limit   0    # 予告として残す構成数（0:予告なし）
  Set use_addtr     0    # 予告が見つからない時のロゴなし予告検出（0:検出しない）
  Set cutec_code    1    # カット動作設定（1:カットあり）
  Set addec_code1   0    # エンドカード検出1（0:検出しない）
  Set addec_code2   0    # エンドカード検出2（0:検出しない）
  Set addec_code3   0    # エンドカード検出3（0:検出しない）
  Set addec_code4   0    # エンドカード検出4（0:検出しない）
  Set addec_code5   0    # エンドカード検出5（0:検出しない）
  Set addtr_limit   0     # 予告が見つからないと判断する必要予告構成数（0:予告なし 1〜:構成数）
  Set cutec_limit   0    # カット位置指定（0:追加なし）
  Set addspt_code1  0
  Set addspt_code2  0
  Set addspt_code3  0
  Set addspt_code4  0
  Set addspt_code5  0
EndIf

# ・最後のロゴ部分はすべてカットする（予告検出前に実行）
If TCLAST
  AutoEdge E -N -1 -Ftail $ftail_st $ftail_ed -code 1003 -period 00:07:00
EndIf

# ・最後のロゴ部分が最後から30秒以内に始まる場合カットする（予告検出前に実行）
If TCLF30
  AutoEdge S -N -1 -Ftail 00:00:00 00:00:30 -code 1003 -period 00:07:00
  # ロゴなし時は最後から30秒を無効にする
  If $NOLOGO
    Set ftail_st      00:00:30  # 最後の予告以降と認識する最終フレームからの位置（起点）
  EndIf
EndIf

# ・最後のロゴ区間が60秒だった時カット（予告検出前に実行）
If TCL60
  AutoEdge E -N -1 -Ftail $ftail_st $ftail_ed -LenP 58.0 62.0 -code 1003 -period 62.0
EndIf

# ・最後のロゴ区間が120秒だった時カット（予告検出前に実行）
If TCL120
  AutoEdge E -N -1 -Ftail $ftail_st $ftail_ed -code 1005 -period 118.0 -maxprd 122.0
EndIf


# ・最後の5秒はカット
If TC5
  Set cutec_code   21   # エンドカードカット動作設定（21:最後period秒構成はカット）
  Set cutec_period 5    # エンドカードカット時に期間指定する場合の秒数
EndIf

# ・最後のロゴの番組宣伝直前15秒未満１構成をカット
If TCB
  Set cutec_code   11   # エンドカードカット動作設定（11:最後の１構成は強制的にカット）
EndIf

# ・エンドカードなしとする
If TCE
  Set cutec_code    1    # カット動作設定（1:カットあり）
  Set addec_code1   0    # エンドカード検出1（0:検出しない）
  Set addec_code2   0    # エンドカード検出2（0:検出しない）
  Set addec_code3   0    # エンドカード検出3（0:検出しない）
  Set addec_code4   0    # エンドカード検出4（0:検出しない）
  Set addec_code5   0    # エンドカード検出5（0:検出しない）
  Set cutec_limit   0    # エンドカードカット位置指定（0:追加なし）
EndIf

# ・ロゴ有無に関係なく15秒未満１構成をエンドカードとして残す
If TREB
  Set cutec_code     1     # カット動作設定（0:カットなし 1:カットあり）
  Set addec_code1    11023 # エンドカード検出1（23:検索90秒以内構成5秒以下 +10000:構成期間13秒以下で候補となる位置に限定）
  Set addec_period1  13    # エンドカード検出1の構成指定秒数(構成5秒の所を秒数変更)
  Set addec_scope1   90    # エンドカード検出1の期間指定時検索終了秒数(code下2桁目=2以上の時使用)
  Set addec_scopen1  0     # エンドカード検出1の期間指定時検索開始秒数(code下2桁目=2以上の時使用)
EndIf

# ・ロゴ有無に関係なく5秒（変更可）以下１構成をエンドカードとして残す
If TRE
  Set cutec_code     1     # カット動作設定（0:カットなし 1:カットあり）
  Set addec_code1    11023 # エンドカード検出1（23:検索90秒以内構成5秒以下 +10000:構成期間13秒以下で候補となる位置に限定）
#  Set addec_period1  5     # エンドカード検出1の構成指定秒数(構成5秒の所を秒数変更)
  Set addec_scope1   90    # エンドカード検出1の期間指定時検索終了秒数(code下2桁目=2以上の時使用)
  Set addec_scopen1  0     # エンドカード検出1の期間指定時検索開始秒数(code下2桁目=2以上の時使用)
EndIf

# ・エンドカードはロゴありのみ検出
If TLE
  Set cutec_code    1     # カット動作設定（0:カットなし 1:カットあり）
  Set addec_code1   0     # エンドカード検出1-優先度1
  Set addec_code2   11223 # エンドカード検出2-優先度2
  Set addec_code3   11223 # エンドカード検出3-優先度3
  Set addec_code4   11203 # エンドカード検出4-優先度4
  Set addec_code5   0     # エンドカード検出5-優先度5
  Set addec_period1 5     # エンドカード検出1の構成指定秒数
  Set addec_period2 5     # エンドカード検出2の構成指定秒数
  Set addec_period3 3     # エンドカード検出3の構成指定秒数
  Set addec_period4 10    # エンドカード検出4の構成指定秒数
  Set addec_period5 5     # エンドカード検出5の構成指定秒数
EndIf

# ・番組提供認識をしない
If TCSP ASPN
  Set addspt_code1  0
  Set addspt_code2  0
  Set addspt_code3  0
  Set addspt_code4  0
  Set addspt_code5  0
EndIf

# ・ロゴ通りに出力
If TN
  If !$NOLOGO
    Set cuttr_code   0
    Set use_addtr    0
    Set cutec_code   0
    Set addec_code1  0
    Set addec_code2  0
    Set addec_code3  0
    Set addec_code4  0
    Set addec_code5  0
    Set addspt_code1  0
    Set addspt_code2  0
    Set addspt_code3  0
    Set addspt_code4  0
    Set addspt_code5  0
  EndIf
EndIf

# ・TWTRLOW   : 予告検出推測は弱くする
If TWTRLOW
  Set use_addtr     0     # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
  Set use_autocmtr  0     # ロゴなし時の予告検出拡張（0:なし 1:あり）
EndIf

# ・TPTRLIM0 : 予告構成数=0
If TPTRLIM0
  Set cuttr_limit   0     # 予告と認識させる構成数（0:予告なし 1〜:構成数）
  Set use_addtr     0     # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
  Set use_autocmtr  0     # ロゴなし時の予告検出拡張（0:なし 1:あり）
EndIf

# ・TPTRLIM2 : 予告構成数=2
If TPTRLIM2
  Set cuttr_limit   2     # 予告と認識させる構成数（0:予告なし 1〜:構成数）
  Set addtr_limit   2     # 予告が見つからないと判断する必要予告構成数（0:制限なし 1〜:構成数）
EndIf

#   TPCUTTR0  : 予告等終了後のロゴ付き15秒単位構成を残す
If TPCUTTR0
  Set param_cuttr   0     # 予告等終了後のロゴ付き15秒単位構成（0:残す 1:カット）
  SetParam CutTR    $param_cuttr
EndIf

#   TPCUTTR1  : 予告等終了後のロゴ付き15秒単位構成をカット
If TPCUTTR1
  Set param_cuttr   1     # 予告等終了後のロゴ付き15秒単位構成（0:残す 1:カット）
  SetParam CutTR    $param_cuttr
EndIf

# ・予告部分のロゴ開始直後15秒はカット（予告検出前に実行）
If TSC15
  AutoEdge S -Ftail $ftail_st $ftail_ed -code 2 -period 15 -LenN 00:00:15 00:02:00
EndIf

# ・予告部分のロゴ終了直前15秒はカット（予告検出前に実行）
If TEC15
#  AutoEdge E -Ftail $ftail_st $ftail_ed -code 2 -period 15 -LenP 00:00:15 -1
  AutoEdge E -Ftail $ftail_st $ftail_ed -code 5 -period 14 -maxprd 16 -LenP 00:00:15 -1
EndIf

# ・予告から60秒離れた所にある15秒構成を番組提供が見つからない時残す
If TWSP15R60
  Set addspt_code5   36   # 最後番組提供-優先度5
  Set addspt_scope   60   # 最後番組提供の期間指定時検索秒数
EndIf

# ・予告が見つからない時のロゴなし予告検出に15秒構成を含める
If TWTR15R60
  Set addtr_code1    25   # 予告が見つからない時のロゴなし予告検出1（0:検出しない 1:隣接15秒未満 25:検索60秒地点構成指定秒）
  Set addtr_period1  1    # ロゴなし予告検出1の期間指定時構成最小秒数(code下1桁=5の時)
  Set addtr_maxprd1  15   # ロゴなし予告検出1の期間指定時構成最大秒数(code下1桁=5の時)
EndIf

# ・予告が見つからない時のロゴなし予告検出に15秒構成を含める（次の構成が10秒、検索範囲は広げる）
If TWTR15A
  Set addtr_code1    1025 # 予告が見つからない時のロゴなし予告検出1（0:検出しない 1:隣接15秒未満 25:検索60秒地点構成指定秒）
  Set addtr_code2    1025 # 予告が見つからない時のロゴなし予告検出1（0:検出しない 1:隣接15秒未満 25:検索60秒地点構成指定秒）
  Set addtr_period1  3    # ロゴなし予告検出1の期間指定時構成最小秒数(code下1桁=5の時)
  Set addtr_period2  3    # ロゴなし予告検出1の期間指定時構成最小秒数(code下1桁=5の時)
  Set addtr_maxprd1  10   # ロゴなし予告検出1の期間指定時構成最大秒数(code下1桁=5の時)
  Set addtr_maxprd2  15   # ロゴなし予告検出1の期間指定時構成最大秒数(code下1桁=5の時)
  Set addtr_scope    125  # 予告が見つからない時のロゴなし予告検出検索秒数（最大）
  Set addtr_scopen   0    # 予告が見つからない時のロゴなし予告検出検索秒数（開始隣接側）
  Set addtr_secnext  10   # 次の構成を秒数指定で限定する（0の時指定しない）
EndIf

# ・ロゴ有無に関係なく5秒１構成をエンドカードとして残す（ロゴから３分以内）
If TRE5R180
  Set cutec_code     1     # カット動作設定（0:カットなし 1:カットあり）
  Set addec_code1    1022  # エンドカード検出1（22:検索指定秒以内構成5秒）
  Set addec_period1  5     # エンドカード検出1の構成指定秒数(構成5秒の所を秒数変更)
  Set addec_scope1   180   # エンドカード検出1の期間指定時検索終了秒数(code下2桁目=2以上の時使用)
  Set addec_scopen1  0     # エンドカード検出1の期間指定時検索開始秒数(code下2桁目=2以上の時使用)
EndIf

#
# ・ロゴなし時だけの動作
#
If $NOLOGO
# ・最終ロゴ扱い終了直後の構成15秒を予告（２回検索）
  If TRTRNL15R0
    Set use_add0tr      1      # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
    Set add0tr_code1    1102   # 後側(1000)ロゴなし(100)隣接指定期間(2)
    Set add0tr_code2    1102   # 後側(1000)ロゴなし(100)隣接指定期間(2)
    Set add0tr_period1  15     # 指定期間秒数
    Set add0tr_period2  15     # 指定期間秒数
    Set use_addtr       1      # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
    Set addtr_code1     1102   # 後側(1000)ロゴなし(100)隣接指定期間(2)
    Set addtr_code2     1102   # 後側(1000)ロゴなし(100)隣接指定期間(2)
    Set addtr_period1   15     # 指定期間秒数
    Set addtr_period2   15     # 指定期間秒数
  EndIf
# ・最終ロゴ扱い終了直後の構成15秒または30秒を予告
  If TRTRNLR0
    Set use_add0tr      1      # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
    Set add0tr_code1    1102   # 後側(1000)ロゴなし(100)隣接指定期間(2)
    Set add0tr_code2    1102   # 後側(1000)ロゴなし(100)隣接指定期間(2)
    Set add0tr_period1  15     # 指定期間秒数
    Set add0tr_period2  30     # 指定期間秒数
  EndIf
# ・最終ロゴ扱い終了から30秒離れた構成15秒または30秒を予告
  If TRTRNLR30
    Set use_add0tr      1      # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
    Set add0tr_code1    1132   # 後側(1000)ロゴなし(100)指定検索位置(30)指定期間(2)
    Set add0tr_code2    1132   # 後側(1000)ロゴなし(100)指定検索位置(30)指定期間(2)
    Set add0tr_period1  15     # 指定期間秒数
    Set add0tr_period2  30     # 指定期間秒数
    Set add0tr_scope    30     # 指定検索位置秒数
  EndIf
# ・最終ロゴ扱い終了から60秒離れた構成15秒または30秒を予告
  If TRTRNLR60
    Set use_add0tr      1      # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
    Set add0tr_code1    1132   # 後側(1000)ロゴなし(100)指定検索位置(30)指定期間(2)
    Set add0tr_code2    1132   # 後側(1000)ロゴなし(100)指定検索位置(30)指定期間(2)
    Set add0tr_period1  15     # 指定期間秒数
    Set add0tr_period2  30     # 指定期間秒数
    Set add0tr_scope    60     # 指定検索位置秒数
  EndIf
# ・最終ロゴ扱い終了から90秒離れた構成15秒または30秒を予告
  If TRTRNLR90
    Set use_add0tr      1      # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
    Set add0tr_code1    1132   # 後側(1000)ロゴなし(100)指定検索位置(30)指定期間(2)
    Set add0tr_code2    1132   # 後側(1000)ロゴなし(100)指定検索位置(30)指定期間(2)
    Set add0tr_period1  15     # 指定期間秒数
    Set add0tr_period2  30     # 指定期間秒数
    Set add0tr_scope    90     # 指定検索位置秒数
  EndIf
# ・最終ロゴ扱い終了から120秒離れた構成15秒または30秒を予告
  If TRTRNLR120
    Set use_add0tr      1      # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
    Set add0tr_code1    1132   # 後側(1000)ロゴなし(100)指定検索位置(30)指定期間(2)
    Set add0tr_code2    1132   # 後側(1000)ロゴなし(100)指定検索位置(30)指定期間(2)
    Set add0tr_period1  15     # 指定期間秒数
    Set add0tr_period2  30     # 指定期間秒数
    Set add0tr_scope    120    # 指定検索位置秒数
  EndIf
# ・最終ロゴ扱い終了から150秒離れた構成15秒または30秒を予告
  If TRTRNLR150
    Set use_add0tr      1      # 予告が見つからない時のロゴなし予告検出（0:検出しない 1:検出する）
    Set add0tr_code1    1132   # 後側(1000)ロゴなし(100)指定検索位置(30)指定期間(2)
    Set add0tr_code2    1132   # 後側(1000)ロゴなし(100)指定検索位置(30)指定期間(2)
    Set add0tr_period1  15     # 指定期間秒数
    Set add0tr_period2  30     # 指定期間秒数
    Set add0tr_scope    150    # 指定検索位置秒数
  EndIf
EndIf

# ・ロゴなしでのANIMAX補正
If NLANIMAX
  # 先頭の10または5秒、CM直前の5秒はカット
  AutoEdge S -Fhead -00:01:00 00:02:00 -code 1022 -period 10 -LenN 00:01:15 -1
  AutoEdge S -Fhead -00:01:00 00:02:00 -code 1022 -period 5  -LenN 00:01:15 -1 -else
  AutoEdge E -code 1022 -period 5 -LenP 00:03:00 -1
  Set addtr_maxprd2  30    # ロゴなし予告検出2の期間指定時構成最大秒数(code下1桁=5の時)
EndIf


#***************** フラグ内容 ********************************************
#   ALSP   : 番組提供はロゴありに限定
#*************************************************************************
# ・番組提供はロゴありに限定（先頭部分）３桁目のみ変更で+1,+2を消して+2を改めて追加
If ALSP
  EvalNum addsph_code1  200+(${addsph_code1}/1000*1000)+(${addsph_code1}%100)+((${addsph_code1}/100%10&4)*100)
  EvalNum addsph_code2  200+(${addsph_code2}/1000*1000)+(${addsph_code2}%100)+((${addsph_code2}/100%10&4)*100)
  EvalNum addsph_code3  200+(${addsph_code3}/1000*1000)+(${addsph_code3}%100)+((${addsph_code3}/100%10&4)*100)
  EvalNum addsph_code4  200+(${addsph_code4}/1000*1000)+(${addsph_code4}%100)+((${addsph_code4}/100%10&4)*100)
  EvalNum addsph_code5  200+(${addsph_code5}/1000*1000)+(${addsph_code5}%100)+((${addsph_code5}/100%10&4)*100)
EndIf
# ・番組提供はロゴありに限定（最後部分）
If ALSP
  EvalNum addspt_code1  200+(${addspt_code1}/1000*1000)+(${addspt_code1}%100)+((${addspt_code1}/100%10&4)*100)
  EvalNum addspt_code2  200+(${addspt_code2}/1000*1000)+(${addspt_code2}%100)+((${addspt_code2}/100%10&4)*100)
  EvalNum addspt_code3  200+(${addspt_code3}/1000*1000)+(${addspt_code3}%100)+((${addspt_code3}/100%10&4)*100)
  EvalNum addspt_code4  200+(${addspt_code4}/1000*1000)+(${addspt_code4}%100)+((${addspt_code4}/100%10&4)*100)
  EvalNum addspt_code5  200+(${addspt_code5}/1000*1000)+(${addspt_code5}%100)+((${addspt_code5}/100%10&4)*100)
EndIf

#***************** ここまでフラグ内容 *****************

#--- ファイル最後部分のカット設定 ---
AutoCut TR -Ftail $ftail_st $ftail_ed -code $cuttr_code+$cuttr_addcode -limit $cuttr_limit -trscope $cuttr_trscope -tr1stprd $cuttr_tr1stprd
If use_add0tr
  AutoAdd TR -Ftail $ftail_st $ftail_ed -code $add0tr_code1 -limit $add0tr_limit -period $add0tr_period1 -maxprd $add0tr_maxprd1 -scope $add0tr_scope -scopen $add0tr_scopen -secnext $add0tr_secnext
  AutoAdd TR -Ftail $ftail_st $ftail_ed -code $add0tr_code2 -limit $add0tr_limit -period $add0tr_period2 -maxprd $add0tr_maxprd2 -scope $add0tr_scope -scopen $add0tr_scopen -secnext $add0tr_secnext -else
EndIf
If use_addtr
  AutoAdd TR -Ftail $ftail_st $ftail_ed -code $addtr_code1 -limit $addtr_limit -period $addtr_period1 -maxprd $addtr_maxprd1 -scope $addtr_scope -scopen $addtr_scopen -secnext $addtr_secnext
  AutoAdd TR -Ftail $ftail_st $ftail_ed -code $addtr_code2 -limit $addtr_limit -period $addtr_period2 -maxprd $addtr_maxprd2 -scope $addtr_scope -scopen $addtr_scopen -secnext $addtr_secnext
EndIf
AutoAdd SP -Ftail $ftail_st $ftail_ed -code $addspt_code1 -limit 1 -scope $addspt_scope -scopen $addspt_scopen -period $addspt_period1 -maxprd $addspt_maxprd1
AutoAdd SP -Ftail $ftail_st $ftail_ed -code $addspt_code2 -limit 1 -scope $addspt_scope -scopen $addspt_scopen
AutoAdd SP -Ftail $ftail_st $ftail_ed -code $addspt_code3 -limit 1 -scope $addspt_scope -scopen $addspt_scopen
AutoAdd SP -Ftail $ftail_st $ftail_ed -code $addspt_code4 -limit 1 -scope $addspt_scope -scopen $addspt_scopen
AutoAdd SP -Ftail $ftail_st $ftail_ed -code $addspt_code5 -limit 1 -scope $addspt_scope -scopen $addspt_scopen
AutoAdd EC -Ftail $ftail_st $ftail_ed -code $addec_code1 -limit 1 -period $addec_period1 -scope $addec_scope1 -scopen $addec_scopen1 -trsumprd $addec_trsumprd
AutoAdd EC -Ftail $ftail_st $ftail_ed -code $addec_code2 -limit 1 -period $addec_period2 -scope $addec_scope2 -scopen $addec_scopen2 -trsumprd $addec_trsumprd
AutoAdd EC -Ftail $ftail_st $ftail_ed -code $addec_code3 -limit 1 -period $addec_period3 -scope $addec_scope3 -scopen $addec_scopen3 -trsumprd $addec_trsumprd
AutoAdd EC -Ftail $ftail_st $ftail_ed -code $addec_code4 -limit 1 -period $addec_period4 -scope $addec_scope4 -scopen $addec_scopen4 -trsumprd $addec_trsumprd
AutoAdd EC -Ftail $ftail_st $ftail_ed -code $addec_code5 -limit 1 -period $addec_period5 -scope $addec_scope5 -scopen $addec_scopen5 -trsumprd $addec_trsumprd
AutoCut EC -Ftail $ftail_st $ftail_ed -code $cutec_code -limit $cutec_limit -period $cutec_period

If use_autocmtr
  If $NOLOGO
    AutoAdd TR -Ftail $ftail_st $ftail_ed -code $acmtr_code1 -limit $addtr_limit -period $acmtr_period -scope $acmtr_scope1
    AutoAdd TR -Ftail $ftail_st $ftail_ed -code $acmtr_code1 -limit $addtr_limit -period $acmtr_period -scope $acmtr_scope2
    AutoAdd TR -Ftail $ftail_st $ftail_ed -code $acmtr_code2 -limit $addtr_limit -period $acmtr_period
  EndIf
EndIf

#----------------------------------------------
# 各ロゴの開始・終了位置カット
#----------------------------------------------
Default edgest_frmmin 00:02:00  # 各ロゴ開始部分のカット対象とするロゴ最低期間
Default edgeed_frmmin 00:02:00  # 各ロゴ終了部分のカット対象とするロゴ最低期間
Default fmid_st       00:10:00  # 中間部分の認識開始位置
Default fmid_ed       00:10:00  # 中間部分の認識終了位置

#***************** フラグ内容 ********************************************
#   SC15   : 途中のロゴ開始直後に15秒不要部分ありカットする
#   EC15   : 途中のロゴ終了直前に15秒不要部分ありカットする
#   SC30HT : 途中（中間あたり）のロゴ開始直後に30秒不要部分ありカットする
#   EC30HT : 途中（中間あたり）のロゴ終了直前に30秒不要部分ありカットする
#*************************************************************************
# ・途中番宣が入っている場合のカット（ロゴ開始直後）
If SC15
  Set edgest_code   2
  Set edgest_period 15
  Set edgest_frmmin 00:02:00
EndIf

# ・途中番宣が入っている場合のカット（ロゴ終了直前）
If EC15
  Set edgeed_code   2
  Set edgeed_period 15
  Set edgeed_frmmin 00:02:00
EndIf

# ・途中30秒番宣が中間あたりに入っている場合のカット（ロゴ開始直後）
If SC30HT
  Set fmid_st    00:10:00
  Set fmid_ed    00:08:00
  AutoEdge S -Fmid $fmid_st $fmid_ed -code 1002 -period 30 -LenN $edgest_frmmin -1
EndIf

# ・途中30秒番宣が中間あたりに入っている場合のカット（ロゴ終了直前）
If EC30HT
  Set fmid_st    00:10:00
  Set fmid_ed    00:08:00
  AutoEdge E -Fmid $fmid_st $fmid_ed -code 1002 -period 30 -LenP $edgeed_frmmin -1
EndIf

#***************** ここまでフラグ内容 *****************

#--- ロゴの開始・終了位置のカット ---
AutoEdge S -code $edgest_code -period $edgest_period -LenN $edgest_frmmin -1
AutoEdge E -code $edgeed_code -period $edgeed_period -LenP $edgeed_frmmin -1
